dbrp "printer"."autogen"

// This TICKscript monitors the three temperatures for a 3d printing job,
// and triggers alerts if the temperatures start to experience abnormal behavior.


// Select the temperatures measurements
var data = stream
    |from()
        .measurement('temperatures')
        .where(lambda: isPresent("hotend"))
    |window()
        .period(30s)
        .every(30s)
        .align()
    |mean('hotend')
      .as('average_hotend')
    |window()
        .period(5m)
        .every(5m)
        .align()

var udf_response = data
    //Run our tTest UDF on the hotend temperature
    @ads()
        // specify the hotend field
        .field('average_hotend')

var write_score = udf_response
 	  |influxDBOut()
  	 	 .database('printer')
  		 .measurement('temperatures')

var write_anomaly_meta = udf_response
    |where(lambda: isPresent("anomaly_ends"))
    |influxDBOut()
        .database('printer')
        .measurement('anomaly_period_meta')
